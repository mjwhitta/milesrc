# If not running interactively, don't do anything
[[ $- == *i* ]] || return

# My custom prompt. I've separated this so I can easily customize and
# share it.

# {{{ Functions
# Get color escape codes
# $1 = fg
# $2 = bg
function __milesrc_colors() {
    [[ $# -le 2 ]] || return

    local bgcode=""
    local fgcode="\e[0m"

    if [[ $# -ge 1 ]]; then
        local fgclr="$1"
        case "$fgclr" in
            "black") fgcode="\e[30m" ;;
            "red") fgcode="\e[31m" ;;
            "green") fgcode="\e[32m" ;;
            "yellow") fgcode="\e[33m" ;;
            "blue") fgcode="\e[34m" ;;
            "magenta") fgcode="\e[35m" ;;
            "cyan") fgcode="\e[36m" ;;
            "white") fgcode="\e[37m" ;;
            "light_black") fgcode="\e[90m" ;;
            "light_red") fgcode="\e[91m" ;;
            "light_green") fgcode="\e[92m" ;;
            "light_yellow") fgcode="\e[93m" ;;
            "light_blue") fgcode="\e[94m" ;;
            "light_magenta") fgcode="\e[95m" ;;
            "light_cyan") fgcode="\e[96m" ;;
            "light_white") fgcode="\e[97m" ;;
        esac
    fi

    if [[ $# -eq 2 ]]; then
        local bgclr="$2"
        case "$bgclr" in
            "black") bgcode="\e[40m" ;;
            "red") bgcode="\e[41m" ;;
            "green") bgcode="\e[42m" ;;
            "yellow") bgcode="\e[43m" ;;
            "blue") bgcode="\e[44m" ;;
            "magenta") bgcode="\e[45m" ;;
            "cyan") bgcode="\e[46m" ;;
            "white") bgcode="\e[47m" ;;
            "light_black") bgcode="\e[100m" ;;
            "light_red") bgcode="\e[101m" ;;
            "light_green") bgcode="\e[102m" ;;
            "light_yellow") bgcode="\e[103m" ;;
            "light_blue") bgcode="\e[104m" ;;
            "light_magenta") bgcode="\e[105m" ;;
            "light_cyan") bgcode="\e[106m" ;;
            "light_white") bgcode="\e[107m" ;;
            *) ;;
        esac
    fi

    case "$MILESRC_SHELL" in
        *"bash") echo -en "\\[$fgcode$bgcode\\]" ;;
        *"zsh") echo -en "%{$fgcode$bgcode%}" ;;
    esac
}

# Conditional rand value
# $1 = exit status
# $2 = rand range if exit status is0
# $3 = rand range otherwise
function __milesrc_conditional_rand() {
    if [[ $# -ne 3 ]]; then
        echo -n "0"
        return
    fi

    local rand="0"
    case "$1" in
        "0") let "rand = RANDOM % $2" ;;
        *) let "rand = RANDOM % $3" ;;
    esac
    echo -n "$rand"
}

# Current working directory
# $1 = the width needed to truncate the cwd
function __milesrc_cwd() {
    if [[ $# -ne 1 ]]; then
        pwd
        return
    fi

    local cwd="$(
        pwd | sed -r -e "s#$HOME#\~#" -e "s#/# / #g" -e "s/^ //"
    )"
    local length="$(echo -n "$cwd" | wc -m)"
    local offset="0"
    local width="$1"

    if [[ $length -le $width ]]; then
        let "offset = $width - $length - 1"
        echo -n "$cwd"
        for i in $(seq 0 $offset); do echo -n " "; done; unset i
    else
        let "offset = $length - $width"
        echo -n "${cwd:$offset:$width}" | sed -r "s/^.... ?/\.\.\. /"
    fi
}

# Exit status
# $1 = last exit status
function __milesrc_exit_status() {
    [[ $# -eq 1 ]] || return

    local status_symbol="$1"
    local rand="$(__milesrc_conditional_rand $1 3 6)"

    case "$MILESRC_PROMPT_EMOTICONS" in
        "") [[ $status_symbol -gt 0 ]] || status_symbol="" ;;
        "unsupported")
            if [[ $status_symbol -eq 0 ]]; then
                case "$rand" in
                    "0") status_symbol="(^‿^)" ;;
                    "1") status_symbol="(ツ)" ;;
                    *) status_symbol="¯\_(ツ)_/¯" ;;
                esac
            else
                case "$rand" in
                    "0") status_symbol="┻━┻ ︵ヽ(°□°)ﾉ︵ ┻━┻ " ;;
                    "1") status_symbol="(╯°□°)╯︵┻━┻ " ;;
                    "2") status_symbol="(ಠ_ಠ)" ;;
                    "3") status_symbol="(ಠ╭╮ಠ)" ;;
                    "4") status_symbol="(ಥ_ಥ)" ;;
                    *) status_symbol="(ง'̀-'́)ง" ;;
                esac
            fi
            ;;
        *)
            case "$status_symbol" in
                "0") status_symbol="(^‿^)" ;;
                *) status_symbol="┻━┻ ︵ヽ(°□°)ﾉ︵ ┻━┻ " ;;
            esac
            ;;
    esac

    echo -n "$status_symbol"
}

# Git info
function __milesrc_git_status() {
    [[ -n $(installed git) ]] || return
    [[ -n $(installed perl) ]] || return

    local git_dir="$(git rev-parse --show-toplevel 2>/dev/null)"
    [[ -n $git_dir ]] || return
    [[ -z $(\grep -s "$git_dir" $HOME/.git.nostatus) ]] || return

    local ignore="--ignore-submodules=dirty"
    local git_status="$(git status -b --porcelain $ignore)"

    local branch_name="$(
        echo "$git_status" | \
        perl -lne '/^##(.*\s+on)?\s+([^.]+)/ && print $2'
    )"
    [[ -n $branch_name ]] || return
    local git="$branch_name"

    local ahead="$(
        echo "$git_status" | \
        perl -lne '/ahead ([0-9]+)/ && print $1'
    )"
    [[ -z $ahead ]] || git="$git ↑$ahead"

    local behind="$(
        echo "$git_status" | \
        perl -lne '/behind ([0-9]+)/ && print $1'
    )"
    [[ -z $behind ]] || git="$git ↓$behind"

    local num_diffs="$(
        echo "$git_status" | \grep -cEv "^ *(##|\?\?|st)"
    )"
    local untracked="$(echo "$git_status" | \grep -cE "^ *(\?\?|st)")"
    case "$num_diffs" in
        "0") ;;
        *) git="$git +$num_diffs" ;;
    esac

    [[ $untracked -eq 0 ]] || git="$git ..."
    if [[ $num_diffs -eq 0 ]] &&
       [[ $untracked -eq 0 ]] &&
       [[ -z $ahead$behind ]]
    then
        git="$git ✓"
    fi

    echo -n "$git"
}

# Hostname and related metadata
function __milesrc_host() {
    echo -n "$USER@$(uname -n | awk -F "." '{print $1}')"
}

# Vi mode for the simulated right prompt
function __milesrc_mode() {
    case "$MILESRC_SHELL" in
        *"bash") ;;
        *"zsh") echo -n "$MILESRC_MODE" ;;
    esac
}

# Prompt PS1 string
# $1 = last exit status
function __milesrc_prompt_ps1() {
    [[ $# -eq 1 ]] || return

    local last_status="$1"

    # Exit status
    local exit="$(__milesrc_exit_status $last_status)"

    # Host
    local host="$(__milesrc_host)"

    # Git
    local git="$(__milesrc_git_status)"

    # CWD
    local width="$COLUMNS"
    [[ -z $exit ]] || let "width -= $(echo -n " $exit " | wc -m)"
    [[ -z $host ]] || let "width -= $(echo -n " $host " | wc -m)"
    [[ -z $git ]] || let "width -= $(echo -n " $git " | wc -m)"
    case "$MILESRC_SHELL" in
        *"bash") let "width -= 3" ;;
        *"zsh") let "width -= 17" ;;
    esac
    [[ $width -ge 0 ]] || let "width = 0"
    local cwd="$(__milesrc_cwd $width)"

    # Prompt symbol
    local sym="\$"
    [[ -z $(id | \grep "uid=0") ]] || sym="#"

    # Put it all together
    case "$last_status" in
        "0")
            if [[ -n $exit ]]; then
                echo -n "$(__milesrc_colors white green) $exit $reset"
            fi
            ;;
        *) echo -n "$(__milesrc_colors white red) $exit $reset" ;;
    esac
    echo -n "$(__milesrc_colors white blue) $host $reset"
    if [[ -n $git ]]; then
        echo -n "$(__milesrc_colors light_black white) $git $reset"
    fi
    echo -n "$(__milesrc_colors white light_black) $cwd $reset"
    echo "$(__milesrc_mode)"
    echo -n "$(__milesrc_colors white light_black)$sym $reset "
}

# Set prompt
function __milesrc_prompt() {
    local last_status="$?"
    if [[ -n $MILESRC_THEME_PROMPT ]]; then
        case "$MILESRC_SHELL" in
            *"bash")
                if [[ -z $MILESRC_OLD_PROMPT ]]; then
                    export MILESRC_OLD_PROMPT="$PS1"
                fi
                export PS1="$(__milesrc_prompt_ps1 $last_status)"
                ;;
            *"zsh")
                if [[ -z $MILESRC_OLD_PROMPT ]]; then
                    export MILESRC_OLD_PROMPT="$PROMPT"
                fi
                export MILESRC_MODE="$VI_INS"
                export PROMPT="$(__milesrc_prompt_ps1 $last_status)"
                ;;
        esac
    else
        case "$MILESRC_SHELL" in
            *"bash")
                if [[ -n $MILESRC_OLD_PROMPT ]]; then
                    export PS1="$MILESRC_OLD_PROMPT"
                    unset MILESRC_OLD_PROMPT
                fi
                ;;
            *"zsh")
                if [[ -n $MILESRC_OLD_PROMPT ]]; then
                    export PROMPT="$MILESRC_OLD_PROMPT"
                    unset MILESRC_OLD_PROMPT
                fi
                ;;
        esac
    fi
}
# }}}

# {{{ Prompt
# Right prompt stuff
export VI_CMD="$(__milesrc_colors white blue)    NORMAL    $reset"
export VI_INS="$(__milesrc_colors white green) -- INSERT -- $reset"

# For convenience
reset="$(__milesrc_colors)"

case "$MILESRC_SHELL" in
    *"bash")
        # set vi-cmd-mode-string "    NORMAL    "
        # set vi-ins-mode-string " -- INSERT -- "
        # set show-mode-in-prompt on

        case "$PROMPT_COMMAND" in
            *"__milesrc_prompt;"*) ;;
            *) PROMPT_COMMAND="__milesrc_prompt; $PROMPT_COMMAND" ;;
        esac
        ;;
    *"zsh")
        function zle-keymap-select() {
            local last_status="$?"
            if [[ -n $MILESRC_THEME_PROMPT ]]; then
                case "$KEYMAP" in
                    "main"|"viins") MILESRC_MODE="$VI_INS" ;;
                    "vicmd") MILESRC_MODE="$VI_CMD" ;;
                esac
                export PROMPT="$(__milesrc_prompt_ps1 $last_status)"
            fi
            zle reset-prompt
        }
        zle -N zle-keymap-select

        function zle-line-finish() {
            local last_status="$?"
            if [[ -n $MILESRC_THEME_PROMPT ]]; then
                export MILESRC_MODE="$VI_INS"
                export PROMPT="$(__milesrc_prompt_ps1 $last_status)"
            fi
        }
        zle -N zle-line-finish

        case "${precmd_functions[(r)__milesrc_prompt]}" in
            "__milesrc_prompt") ;;
            *) precmd_functions+=(__milesrc_prompt) ;;
        esac
        ;;
esac
# }}}
