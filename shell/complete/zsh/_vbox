#compdef vbox

[[ -n $(command -v VBoxManage) ]] || return
[[ -n $(command -v perl) ]] || return

local vbox="${words[1]}"
local vms=($($vbox list))
local snapshots=($(
    $vbox snapshot list | perl -lne '/Name: (.+) \(UUID/ && print $1'
))

if [[ $CURRENT -eq 2 ]]; then
    local vbox_options=(
        "destroy:Remove specified VM"
        "halt:Poweroff specified VM or all VMs"
        "help:Display help message"
        "list:List VMs"
        "port:Manage forwarded ports of specified VM or all VMs"
        "ssh:SSH into the specified VM if SSH is port forwarded"
        "ssh-config:Output .ssh/config for the specified VM"
        "snapshot:Manage snapshots of specified VM or all VMs"
        "status:Show the state of specified VM or all VMs"
        "suspend:Save the state of specified VM or all VMs"
        "up:Start specified VM or all VMs"
    )
    _describe "vbox commands" vbox_options "$@"
elif [[ $CURRENT -eq 3 ]]; then
    case "${words[CURRENT - 1]}" in
        "destroy"|"halt"|"ssh"|"ssh-config"|"status"|"suspend")
            _describe "VMs" vms "$@"
            ;;
        "port")
            local vbox_options=(
                "delete:Delete a forwarded port"
                "forward:Forward a port"
                "list:List forwarded ports"
            )
            _describe "port commands" vbox_options "$@"
            ;;
        "snapshot")
            local vbox_options=(
                "delete:Delete a snapshot"
                "list:List snapshots"
                "restore:Restore a snapshot"
                "take:Take a snapshot"
            )
            _describe "snapshot commands" vbox_options "$@"
            ;;
        "up")
            local vbox_options=(
                "detached:Start as separate process"
                "gui:Don't start headless"
            )
            _describe -t "up commands" "up commands" vbox_options "$@"
            _describe -t "VMs" "VMs" vms "$@"
            ;;
        *) # Do nothing
            ;;
    esac
else
    case "${words[2]}" in
        "destroy"|"halt"|"status"|"suspend"|"up")
            _describe "VMs" vms "$@"
            ;;
        "port")
            case "${words[3]}" in
                "delete")
                    [[ $CURRENT -gt 4 ]] && _describe "VMs" vms "$@"
                    ;;
                "forward")
                    [[ $CURRENT -gt 6 ]] && _describe "VMs" vms "$@"
                    ;;
                *) _describe "VMs" vms "$@"
                    ;;
            esac
            ;;
        "snapshot")
            case "${words[3]}" in
                "delete"|"restore")
                    if [[ $CURRENT -eq 4 ]]; then
                        _describe "snapshots" snapshots "$@"
                    elif [[ $CURRENT -gt 4 ]]; then
                        _describe "VMs" vms "$@"
                    fi
                    ;;
                *) _describe "VMs" vms "$@"
                    ;;
            esac
            ;;
        *) # Do nothing
            ;;
    esac
fi
